
##
# Propose moving from center to health or junk
##
sp {junk-health*propose*move
   (state <s> ^name junk-health
              ^direction <d>
              ^location center)
   (<d> ^name {<dir> <> center} ^negreward <neg>)
-->
   (<s> ^operator <op> +)
   (<op> ^name move
         ^dir <dir>
     ^previous center
     ^discount <neg>)
}

##
# Propose moving from health back to center
##
sp {junk-health*propose*move*health-to-center
   (state <s> ^name junk-health
              ^location health)
-->
   (<s> ^operator <op> +)
   (<op> ^name move
         ^dir center
     ^previous health
     ^discount 0)
}

##
# Propose moving from junk back to center
##
sp {junk-health*propose*move*junk-to-center
   (state <s> ^name junk-health
              ^location junk)
-->
   (<s> ^operator <op> +)
   (<op> ^name move
         ^dir center
     ^previous junk
     ^discount 0)
}

##
# Soar-RL rule for moving towards health food
##
sp {junk-health*rl*health
   (state <s> ^name junk-health
              ^operator <op> +)
   (<op> ^name move
         ^dir health
     ^previous center)
-->
   (<s> ^operator <op> = 0)
}

##
# Soar-RL rule for moving towards junk food
##
sp {junk-health*rl*junk
   (state <s> ^name junk-health
              ^operator <op> +)
   (<op> ^name move
         ^dir junk
     ^previous center)
-->
   (<s> ^operator <op> = 0)
}

##
# Soar-RL rule for moving towards center
##
sp {junk-health*rl*center-previous-health
   (state <s> ^name junk-health
              ^operator <op> +)
   (<op> ^name move
         ^dir center
     ^previous health)
-->
   (<s> ^operator <op> = 0)
}

##
# Soar-RL rule for moving towards center
##
sp {junk-health*rl*center-previous-junk
   (state <s> ^name junk-health
              ^operator <op> +)
   (<op> ^name move
         ^dir center
     ^previous junk)
-->
   (<s> ^operator <op> = 0)
}

##
# When a direction is chosen:
# - change location
# - output choice
##
sp {apply*move
   (state <s> ^operator <op>
              ^location <current>
        ^previous <prev>
        ^checkup <check>
        ^counter <count> < <check>
        ^discount-apply <dis>
        ^discount-total <distot>
        ^health <health>
       -^thinking true)
   (<op> ^name move
         ^dir <dir> <> <current>
     ^previous <last>
     ^discount <curdis>)     
-->
   (<s> ^location <current> - <dir>)
   (<s> ^previous <prev> - <last>)
   (<s> ^counter <count> -)
   (<s> ^counter ( + <count> 1))
   (<s> ^discount-apply <dis> - 0)
   (<s> ^discount-total <distot> - (+ <distot> <curdis>))
   (<s> ^health <health> - (+ <health> <curdis>))
   (write (crlf) |Moved: | <dir>)
   (write (crlf) |Health: | <health>)
}

##
# Apply "checkup" if counter is correct value
##
sp {apply*move-and-counter
   (state <s> ^operator <op>
              ^location <current>
        ^previous <prev>
        ^checkup <check>
        ^counter <count> >= <check>
        ^discount-apply <dis>
        ^discount-total <distot>
        ^health <health>
       -^thinking true)
   (<op> ^name move
         ^dir <dir> <> <current>
     ^previous <last>
     ^discount <curdis>)
-->
   (<s> ^location <current> - <dir>)
   (<s> ^previous <prev> - <last>)
   (<s> ^counter <count> -)
   (<s> ^counter 1)
   (<s> ^discount-apply <dis> - (+ <distot> <curdis>))
   (<s> ^discount-total <distot> - 0)  
   (<s> ^health <health> - (+ <health> <curdis>))   
   (write (crlf) |Moved: | <dir>)
   (write (crlf) |Health: | <health>)
}


##
# When a direction is chosen:
# - change location
# - output choice
##
sp {apply*ponder-move
   (state <s> ^operator <op>
              ^location <current>
        ^previous <prev>
        ^checkup <check>
        ^counter <count> < <check>
        ^discount-apply <dis>
        ^discount-total <distot>
        ^health <health>
        ^thinking true
        ^study-reward.<dir> <study-reward>)
   (<op> ^name move
         ^dir <dir> <> <current>
     ^previous <last>
     ^discount <curdis>)     
-->
   (<s> ^location <current> - <dir>)
   (<s> ^previous <prev> - <last>)
   (<s> ^counter <count> -)
   (<s> ^counter ( + <count> 1))
   (<s> ^discount-apply <dis> - <study-reward>)
   (<s> ^discount-total <distot> - <distot>)
   (<s> ^health <health> - (+ <health> <curdis>))
   (write (crlf) |Moved: | <dir>)
   (write (crlf) |Health: | <health>)
}

##
# Apply "checkup" if counter is correct value
##
sp {apply*ponder-move-and-counter
   (state <s> ^operator <op>
              ^location <current>
        ^previous <prev>
        ^checkup <check>
        ^counter <count> >= <check>
        ^discount-apply <dis>
        ^discount-total <distot>
        ^health <health>
        ^thinking true
        ^study-reward.<dir> <study-reward>)
   (<op> ^name move
         ^dir <dir> <> <current>
     ^previous <last>
     ^discount <curdis>)
-->
   (<s> ^location <current> - <dir>)
   (<s> ^previous <prev> - <last>)
   (<s> ^counter <count> -)
   (<s> ^counter 1)
   (<s> ^discount-apply <dis> - (+ <distot> <curdis> <study-reward>))
   (<s> ^discount-total <distot> - 0)  
   (<s> ^health <health> - (+ <health> <curdis>))   
   (write (crlf) |Moved: | <dir>)
   (write (crlf) |Health: | <health>)
}

sp {initial-recall-first-episode
   (state <s> ^start-id <start>
              ^thinking-start <thinking-start>
              ^epmem.present-id <thinking-start>
              ^epmem.command <command>)
-->
   (<command> ^retrieve <start>)
}

sp {general-recall-first-episode
   (state <s> ^start-id <start>
              ^epmem.result.memory-id <thinking-start>
              ^thinking-start <thinking-start>
              ^epmem.command <command>)
-->
   (<command> ^retrieve <start>)
}

sp {recall-next-episode
   (state <s> ^thinking true
              ^thinking-start <thinking-start>
              ^epmem.result.memory-id <memory-id> <> <thinking-start>
              -^epmem.present-id <thinking-start>
              ^epmem.command <command>)
-->
   (<command> ^retrieve (+ 1 <memory-id>))

}

sp {reject-non-repeat-moves
   (state <s> ^operator <op> +
              ^thinking true
              ^epmem.result.retrieved.location <dir>)
   (<op> ^dir <> <dir>)
-->
   (<s> ^operator <op> -)
}